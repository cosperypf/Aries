# NOTE: this script will be called before project() command

# Check if compiled as subproject or not
if(NOT DEFINED PROJECT_NAME)
  set(JUGLANS_IS_TOP_LEVEL ON)
else()
  set(JUGLANS_IS_TOP_LEVEL OFF)
endif()

option(ANDROID "Cross-build Android" OFF)
if (ANDROID)
  if (NOT JUGLANS_ANDROID_NDK)
    if (DEFINED ENV{ANDROID_NDK_r21e} AND USE_QXRCORE)
      set(JUGLANS_ANDROID_NDK $ENV{ANDROID_NDK_r21e})
    elseif (DEFINED ENV{ANDROID_NDK_r17c})
      set(JUGLANS_ANDROID_NDK $ENV{ANDROID_NDK_r17c})
    elseif (DEFINED ENV{ANDROID_NDK_r21e} AND NOT USE_QXRCORE)
      set(JUGLANS_ANDROID_NDK $ENV{ANDROID_NDK_r21e})
    elseif (ANDROID_NDK)
      message(WARNING "Using Android NDK other than r17c may result in error for SNPE")
      set(JUGLANS_ANDROID_NDK ${ANDROID_NDK})
    elseif (DEFINED ENV{ANDROID_NDK})
      message(WARNING "Using Android NDK other than r17c may result in error for SNPE")
      set(JUGLANS_ANDROID_NDK $ENV{ANDROID_NDK})
    endif()
  endif()
  if (NOT JUGLANS_ANDROID_NDK)
    message(FATAL_ERROR "Android NDK not found, check ANDROID_NDK_r17c environment variable")
  endif()
  set(CMAKE_TOOLCHAIN_FILE "${JUGLANS_ANDROID_NDK}/build/cmake/android.toolchain.cmake")
  cmake_path(CONVERT ${CMAKE_TOOLCHAIN_FILE} TO_CMAKE_PATH_LIST CMAKE_TOOLCHAIN_FILE NORMALIZE)
  set(ANDROID_ABI arm64-v8a)
  if (USE_QXRCORE)
    # must be 29 to support qxr camera
    set(ANDROID_PLATFORM android-29)
  else()
    set(ANDROID_PLATFORM android-24)
  endif()
  set(ANDROID_STL c++_shared)  # use same stl with SNPE & qxr
endif()
